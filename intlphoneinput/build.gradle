apply plugin: 'com.android.library'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.googlecode.libphonenumber:libphonenumber:7.2.1'
}


group = 'net.rimoto'  // Maven Group ID for the artifact
version = '0.9.0'

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

apply plugin: 'com.github.dcendents.android-maven'
install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'
                groupId 'net.rimoto'
                artifactId "intlphoneinput"

                // Add your description here
                name "IntlPhoneInput"
                description "A custom view for Android that allows the user to enter his phone number in an elegant and friendly way."
                url "https://github.com/rimoto/IntlPhoneInput"

                // Set your license
                licenses {
                    license {
                        name "The Apache Software License, Version 2.0"
                        url "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }
                developers {
                    developer {
                        name "Rimoto"
                    }
                }
                scm {
                    connection "https://github.com/rimoto/IntlPhoneInput.git"
                    url "https://github.com/rimoto/IntlPhoneInput"
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

apply plugin: 'com.jfrog.bintray'
bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    configurations = ['archives']
    pkg {
        repo = 'maven'
        name = 'IntlPhoneInput'
        userOrg = "rimoto"
        licenses = ['Apache-2.0']
        publish = true
        publicDownloadNumbers = true
        vcsUrl = "https://github.com/rimoto/IntlPhoneInput.git"
        version {
            name = "0.9.1"
            released  = new Date()
            attributes = ['gradle-plugin': 'com.use.less:com.use.less.gradle:gradle-useless-plugin']
            gpg {
                sign = true
            }
        }
    }
}